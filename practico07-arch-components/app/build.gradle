apply plugin: 'com.android.application'

apply plugin: 'kotlin-android'

apply plugin: 'kotlin-android-extensions'

apply plugin: 'kotlin-kapt'

def integrationTests = false
android {
    compileSdkVersion 28

    defaultConfig {
        applicationId "uy.edu.ude.archcomponents"
        minSdkVersion 22
        targetSdkVersion 28
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        // The following argument makes the Android Test Orchestrator run its
        // "pm clear" command after each test invocation. This command ensures
        // that the app's state is completely cleared between tests.
        testInstrumentationRunnerArguments clearPackageData: 'true'

    }
    compileOptions {
        targetCompatibility 1.8
        sourceCompatibility 1.8
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    testOptions {
        unitTests {
            includeAndroidResources = true
            returnDefaultValues = true
            all {
                jacoco.includeNoLocationClasses = true
                test {
                    filter {
                        if (integrationTests.toBoolean()) {
                            includeTestsMatching "*IntegrationTest*"
                            includeTestsMatching "*InstrumentedTest*"
                        } else {
                            includeTestsMatching "*UnitTest"
                            includeTestsMatching "*RobolectricTest"
                            exclude "*IntegrationTest*"
                            exclude "*InstrumentedTest*"
                        }
                    }
                }
            }
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    implementation 'androidx.appcompat:appcompat:1.0.2'
    implementation 'androidx.core:core-ktx:1.0.2'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation 'com.google.android.material:material:1.0.0'

    // Room components
    implementation "androidx.room:room-runtime:2.1.0"
    implementation "androidx.room:room-ktx:2.1.0"
    kapt "androidx.room:room-compiler:2.1.0"
    androidTestImplementation "androidx.room:room-testing:2.1.0"

    // Lifecycle components
    implementation "androidx.lifecycle:lifecycle-extensions:2.1.0-rc01"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:2.1.0-rc01"
    kapt "androidx.lifecycle:lifecycle-compiler:2.1.0-rc01"
    androidTestImplementation "androidx.arch.core:core-testing:2.0.1"
    testImplementation "androidx.arch.core:core-testing:2.0.1"

    // ViewModel Kotlin support
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:2.1.0-rc01"

    // Coroutines
    api "org.jetbrains.kotlinx:kotlinx-coroutines-core:1.2.2"
    api "org.jetbrains.kotlinx:kotlinx-coroutines-android:1.2.2"

    testImplementation 'junit:junit:4.12'
    testImplementation "org.robolectric:robolectric:4.3"
    testImplementation "org.assertj:assertj-core:3.12.2"
    testImplementation 'androidx.test.espresso:espresso-core:3.2.0'
    testImplementation 'androidx.test.espresso:espresso-intents:3.2.0'
    testImplementation 'androidx.test:rules:1.2.0'
    testImplementation 'androidx.test:runner:1.2.0'
    testImplementation 'androidx.test.ext:junit:1.1.1'

    androidTestImplementation "org.assertj:assertj-core:3.12.2"
    androidTestImplementation ('androidx.test.espresso:espresso-core:3.2.0',{
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    androidTestImplementation "androidx.arch.core:core-testing:2.1.0-rc01"
}

// Static analysis
def mainSrc = "$project.projectDir/src/main/java"
def fileFilter = ['**/R.class', '**/R$*.class', '**/BuildConfig.*', '**/Manifest*.*', '**/*Test*.*', 'android/**/*.*']
def debugTree = fileTree(dir: "$project.buildDir/intermediates/javac/debug", excludes: fileFilter) +
        fileTree(dir: "$project.buildDir/tmp/kotlin-classes/debug", excludes: fileFilter)
def configDir = "${project.rootDir}/config"
def reportDir = "${project.buildDir}/reports"

//gradlew detekt
detekt {
    toolVersion = "1.0.0"
    input = files("src/main/java")
    filters = ".*/resources/.*,.*/build/.*"
    reports {
        xml {
            enabled = false
        }
        html {
            enabled = true
            destination = file("$reportDir/detekt.html")
        }
    }
}

//Jacoco
apply plugin: 'jacoco'
jacoco.toolVersion = '0.8.4'
//gradlew jacocoTestReport
task jacocoTestReport(type: JacocoReport, dependsOn: 'testDebugUnitTest') {
    reports {
        xml.enabled = false
        html.enabled = true
    }
    sourceDirectories.setFrom(files([mainSrc]))
    classDirectories.setFrom([debugTree])
    executionData.setFrom(fileTree(dir: project.buildDir, includes: [
            'jacoco/testDebugUnitTest.exec', 'outputs/code-coverage/connected/*coverage.ec'
    ]))
}
//./gradlew jacocoTestCoverageVerification
task jacocoTestCoverageVerification(type: JacocoCoverageVerification, dependsOn: 'jacocoTestReport') {
    sourceDirectories.setFrom(files([mainSrc]))
    classDirectories.setFrom([debugTree])
    executionData.setFrom(files("${buildDir}/jacoco/testDebugUnitTest.exec"))
    violationRules {
        failOnViolation = true
        rule {
            limit {
                minimum = 0.3
            }
        }
    }
}

//Lint ./gradlew lint
android {
    lintOptions {
        abortOnError false
        xmlReport false
        htmlReport true
        lintConfig file("$configDir/lint.xml")
        htmlOutput file("$reportsDir/lint.html")
    }
}

//gradlew ktlintCheck
//gradlew ktlintFormat
apply plugin: "org.jlleitschuh.gradle.ktlint"

ktlint {
    android = true
    debug = true
}